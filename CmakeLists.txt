cmake_minimum_required(VERSION 3.10)
project(city-2048)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(CheckIncludeFile)
include(CheckLibraryExists)

file(GLOB_RECURSE SRC_FILES CONFIGURE_DEPENDS src/cpp/*.cpp)
add_executable(index src/main.cpp ${SRC_FILES})

target_include_directories(index PRIVATE
    ${CMAKE_SOURCE_DIR}/src/include
    ${CMAKE_SOURCE_DIR}/glm
    ${CMAKE_SOURCE_DIR}/stb)

if (EMSCRIPTEN)
    set_target_properties(index PROPERTIES SUFFIX ".html")
    target_include_directories(index PRIVATE ${CMAKE_SOURCE_DIR}/freetype/include)

    set(EMSCRIPTEN_PRELOAD_DIR "${CMAKE_SOURCE_DIR}/shaders")
    set(FONTS_DIR "${CMAKE_SOURCE_DIR}/fonts")
    set(TEXTURES_DIR "${CMAKE_SOURCE_DIR}/textures")

    target_link_options(index PRIVATE
        "-sUSE_GLFW=3"
        "-sUSE_FREETYPE=1"
        "-sFULL_ES3=1"
        "-sASYNCIFY"
        "-sALLOW_MEMORY_GROWTH=1"
        "--preload-file=${EMSCRIPTEN_PRELOAD_DIR}@/shaders"
        "--preload-file=${FONTS_DIR}@/fonts"
        "--preload-file=${TEXTURES_DIR}@/textures"
        "--shell-file=${CMAKE_SOURCE_DIR}/index.html"
        # "-sUSE_WEBGL2=1"
    )

    target_compile_options(index PRIVATE -Werror)
else() # Windows debug

    set(GLFW_ROOT ${CMAKE_SOURCE_DIR}/glfw)
    set(GLEW_ROOT ${CMAKE_SOURCE_DIR}/glew)
    set(FREETYPE_ROOT ${CMAKE_SOURCE_DIR}/freetype)
    set(IMGUI_DIR ${CMAKE_SOURCE_DIR}/imgui)

    target_include_directories(index PRIVATE "${GLFW_ROOT}/include")
    target_link_directories(index PRIVATE "${GLFW_ROOT}/lib")

    target_include_directories(index PRIVATE "${GLEW_ROOT}/include")
    target_link_directories(index PRIVATE "${GLEW_ROOT}/lib")

    target_include_directories(index PRIVATE "${FREETYPE_ROOT}/include")
    target_link_directories(index PRIVATE "${FREETYPE_ROOT}/lib")

    target_include_directories(index PRIVATE
        ${IMGUI_DIR}
        ${IMGUI_DIR}/backends
    )
    target_sources(index PRIVATE
        ${IMGUI_DIR}/imgui.cpp
        ${IMGUI_DIR}/imgui_draw.cpp
        ${IMGUI_DIR}/imgui_tables.cpp
        ${IMGUI_DIR}/imgui_widgets.cpp
        ${IMGUI_DIR}/imgui_demo.cpp
        ${IMGUI_DIR}/backends/imgui_impl_glfw.cpp
        ${IMGUI_DIR}/backends/imgui_impl_opengl3.cpp
    )

    target_compile_definitions(index PRIVATE 
        GLEW_STATIC
        STB_IMAGE_IMPLEMENTATION)

    target_link_libraries(index PRIVATE freetype glfw3 glew32s opengl32 gdi32 winmm)

    add_custom_command(TARGET index POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CMAKE_SOURCE_DIR}/shaders"
            "$<TARGET_FILE_DIR:index>/shaders"
        COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CMAKE_SOURCE_DIR}/fonts"
            "$<TARGET_FILE_DIR:index>/fonts"
        COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CMAKE_SOURCE_DIR}/textures"
            "$<TARGET_FILE_DIR:index>/textures"
    )

    # target_compile_definitions(index PRIVATE RLGL_SHOW_GL_DETAILS)
endif()
